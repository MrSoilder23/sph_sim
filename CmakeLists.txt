cmake_minimum_required(VERSION 3.31)
project(SPH-Sim LANGUAGES C CXX)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

set(PROG_NAME sim)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_SOURCE_DIR}/bin)

file(GLOB_RECURSE SOURCES CONFIGURE_DEPENDS "src/*.cpp")
file(GLOB_RECURSE SHADER_FILES CONFIGURE_DEPENDS "${CMAKE_SOURCE_DIR}/shaders/*")
file(GLOB_RECURSE CONFIG_FILES CONFIGURE_DEPENDS "${CMAKE_SOURCE_DIR}/config/*")

add_library(glad STATIC third_party/glad/glad.c)
find_package(SDL3 CONFIG REQUIRED)
find_package(nlohmann_json CONFIG REQUIRED)
find_package(OpenMP REQUIRED)

add_executable(${PROG_NAME} ${SOURCES})

add_custom_target(
    CopyShadersConfig ALL
    COMMAND ${CMAKE_COMMAND} -E copy_directory
        ${CMAKE_SOURCE_DIR}/shaders
        ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/shaders
    COMMAND ${CMAKE_COMMAND} -E copy_directory
        ${CMAKE_SOURCE_DIR}/config
        ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/config
    DEPENDS ${SHADER_FILES} ${CONFIG_FILES}
    COMMENT "Copying shaders and config..."
)

add_dependencies(${PROG_NAME} CopyShadersConfig)

target_include_directories(glad PUBLIC third_party)
target_include_directories(${PROG_NAME} PRIVATE
    ${CMAKE_SOURCE_DIR}/include
    ${CMAKE_SOURCE_DIR}/third_party    
)
target_link_libraries(${PROG_NAME} PRIVATE 
    glad    
    nlohmann_json::nlohmann_json
    SDL3::SDL3
    OpenMP::OpenMP_C
    OpenMP::OpenMP_CXX
)

enable_testing()
add_subdirectory(tests)
